%{
    #include <stdio.h>
    #include <iostream>
    #include "Utilities.hpp"

    extern int yylex();
    void yyerror(const char * Error);
%}

%token VOID
%token INT
%token BYTE
%token B
%token BOOL
%token OVERRIDE
%token TRUE
%token FALSE
%token RETURN
%token WHILE
%token BREAK
%token CONTINUE
%token SC
%token COMMA
%token LBRACE
%token RBRACE
%token ID
%token NUM
%token STRING

%left OR
%left AND
%left RELOP
%left ADD SUB
%left MUL DIV


%right      NOT
%right      IF
%right      ELSE
%right      ASSIGN

%nonassoc   LPAREN
%nonassoc   RPAREN

%%

Program                     : { $$ = new Program(); }   Funcs   { ProgramFinish(); }
                            ;

Funcs                       : FuncDecl Funcs                                                { $$ = new Funcs(); }
                            |                                                               { $$ = new Funcs(); }
                            ;

FuncDecl                    : OverRide RetType ID LPAREN Formals RPAREN  { $$ = new FuncDecl((RetType*)($2) , ($3) , (Formals*)($5), (OverRide*)($1)); }  LBRACE OpenScope { InsertArgs((Formals*)($5)); } Statements CloseScope { FunctionFinish(); } RBRACE
                            ;

OverRide                    : OVERRIDE  { $$ = new OverRide(true); }
                            |           { $$ = new OverRide(false); }
                            ;

RetType                     : Type                                                          { $$ = new RetType((Type*)($1)); } 
                            | VOID                                                          { $$ = new RetType($1); } 
                            ;

Formals                     : FormalsList                                                   { $$ = new Formals((FormalsList*)($1)); }
                            |                                                               { $$ = new Formals(); }
                            ;

FormalsList                 : FormalDecl COMMA FormalsList                                  { $$ = new FormalsList((FormalDecl*)($1) , (FormalsList*)($3)); }
                            | FormalDecl                                                    { $$ = new FormalsList((FormalDecl*)($1)); }
                            ;

FormalDecl                  : Type ID                                                       { $$ = new FormalDecl((Type*)($1) , $2); }
                            ; 

Statements                  : Statements Statement                                          { $$ = new Statements((Statements*)($1), (Statement*)($2)); }
                            | Statement                                                     { $$ = new Statements((Statement*)($1)); }
                            ;

Statement                   : OStatement                                                    { $$ = $1; }
                            | CStatement                                                    { $$ = $1; }
                            ;

OStatement                  : IF LPAREN IsBool RPAREN OpenScope Label Statement                                                             { $$ = new Statement((Exp*)($3) , (Label*)($6) , (Statement*)($7)); CloseScope(); }                                                         // Bool - Truelabel - Statement
                            | IF LPAREN IsBool RPAREN OpenScope Label CStatement Branch CloseScope ELSE OpenScope Label OStatement          { $$ = new Statement((Exp*)($3) , (Label*)($6) , (Statement*)($7) , (Label*)($12) , (Statement*)($13) , (Branch*)($8)); CloseScope(); }     // Bool - Truelabel - TrueStatement - Falselabel - FalseStatement - Branch
                            | WHILE LPAREN Label IsBool RPAREN OpenScope OpenLoop Label OStatement CloseLoop                                { $$ = new Statement((Exp*)($4) , (Label*)($8) , (Statement*)($9) , (Label*)($3)); CloseScope(); }                                          // Bool - Truelabel - TrueStatement - Looplabel
                            ;

CStatement                  : NIStatement                                                                                                   { ; }
                            | IF LPAREN IsBool RPAREN OpenScope Label CStatement Branch CloseScope ELSE OpenScope Label CStatement          { $$ = new Statement((Exp*)($3) , (Label*)($6) , (Statement*)($7) , (Label*)($12) , (Statement*)($13) , (Branch*)($8)); CloseScope(); }    // Bool - Truelabel - TrueStatement - Falselabel - FalseStatement
                            | WHILE LPAREN Label IsBool RPAREN OpenScope OpenLoop Label CStatement CloseLoop                                { $$ = new Statement((Exp*)($4) , (Label*)($8) , (Statement*)($9) , (Label*)($3)); CloseScope(); }                                         // Bool - Truelabel - TrueStatement - Looplabel
                            ;

NIStatement                 : LBRACE OpenScope Statements CloseScope RBRACE                 { $$ = $3; }
                            | Type ID SC                                                    { $$ = new Statement((Type*)($1) , $2); }
                            | Type ID ASSIGN Exp SC                                         { $$ = new Statement((Type*)($1) , $2 , (Exp*)($4)); }
                            | ID ASSIGN Exp SC                                              { $$ = new Statement($1 , (Exp*)($3)); }
                            | Call SC                                                       { $$ = new Statement((Call*)$1); }
                            | RETURN SC                                                     { $$ = new Statement($1 , true); }
                            | RETURN Exp SC                                                 { $$ = new Statement((Exp*)($2)); }
                            | BREAK SC                                                      { $$ = new Statement($1); }
                            | CONTINUE SC                                                   { $$ = new Statement($1); }
                            ;

Call                        : ID LPAREN ExpList RPAREN                                      { $$ = new Call($1 , (ExpList*)($3)); }
                            | ID LPAREN RPAREN                                              { $$ = new Call($1); }
                            ;

ExpList                     : ExpList COMMA Exp                                             { $$ = new ExpList((Exp*)($3) , (ExpList*)($1)); }
                            | Exp                                                           { $$ = new ExpList((Exp*)($1)); }
                            ;

Type                        : INT                                                           { $$ = new Type($1); }
                            | BYTE                                                          { $$ = new Type($1); }
                            | BOOL                                                          { $$ = new Type($1); }
                            ;

Exp                         : LPAREN Exp RPAREN                                             { $$ = new Exp((Exp*)($2)); }
                            | Exp OR Label Exp                                              { $$ = new Exp((Exp*)($1) , $2 , (Exp*)($4) , "OR", (Label*)$3); }
                            | Exp AND Label Exp                                             { $$ = new Exp((Exp*)($1) , $2 , (Exp*)($4) , "AND", (Label*)$3); }
                            | Exp RELOP Exp                                                 { $$ = new Exp((Exp*)($1) , $2 , (Exp*)($3) , "RELOP", nullptr); }
                            | Exp ADD Exp                                                   { $$ = new Exp((Exp*)($1) , $2 , (Exp*)($3) , "BINOP", nullptr); }
                            | Exp SUB Exp                                                   { $$ = new Exp((Exp*)($1) , $2 , (Exp*)($3) , "BINOP", nullptr); }
                            | Exp MUL Exp                                                   { $$ = new Exp((Exp*)($1) , $2 , (Exp*)($3) , "BINOP", nullptr); }
                            | Exp DIV Exp                                                   { $$ = new Exp((Exp*)($1) , $2 , (Exp*)($3) , "BINOP", nullptr); }
                            | ID                                                            { $$ = new Exp($1); }
                            | Call                                                          { $$ = new Exp((Call*)($1)); }
                            | NUM                                                           { $$ = new Exp($1 , "INT"); }
                            | NUM B                                                         { $$ = new Exp($1 , "BYTE"); }
                            | STRING                                                        { $$ = new Exp($1 , "STRING"); }
                            | TRUE                                                          { $$ = new Exp($1 , "BOOL"); }
                            | FALSE                                                         { $$ = new Exp($1 , "BOOL"); }
                            | NOT Exp                                                       { $$ = new Exp($1 , (Exp*)($2)); }
                            | LPAREN Type RPAREN Exp                                        { $$ = new Exp((Type*)($2) , (Exp*)($4)); }
                            ;

IsBool                      : Exp                                                           { CheckBool((Exp*)($1)); }

OpenScope                   :   { OpenScope(); }
                            ;

CloseScope                  :   { CloseScope(); }
                            ;

OpenLoop                    :   { AddLoop(); }
                            ;

CloseLoop                   :   { RemoveLoop(); }
                            ;

Label                       :   { $$ = new Label(); }
                            ;

Branch                      :   { $$ = new Branch(); }
                            ;
%%

int main()
{
	return yyparse();
}

void yyerror(const char * Error)
{
	output::errorSyn(yylineno);
	exit(0);
}